{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# 1D Wave-equation / linearized shallow-water equation on a periodic domain\n",
    "\n",
    "The one-dimensional shallow-water equations can be linearized and simplified to the 1D wave-equation:\n",
    "\n",
    "$\\partial h/\\partial t + D \\partial u / \\partial x = 0$\n",
    "\n",
    "$\\partial u/\\partial t + g \\partial h / \\partial x = 0$\n",
    "\n",
    "where $h$ denotes the water-height above the reference, $u$ the velocity, $D$ the depth below the reference and $t,x$ time and space."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# goto folder of this notebook (If you move this notebook then also copy the Project.toml file and run instantiate to install the packages)\n",
    "cd(@__DIR__)\n",
    "# load the environment\n",
    "using Pkg \n",
    "Pkg.activate(\".\")  # activate the environment for this notebook\n",
    "# load the packages\n",
    "using OrdinaryDiffEq\n",
    "using ComponentArrays\n",
    "using Plots\n",
    "using JLD2\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# define the model\n",
    "\n",
    "\"\"\"\n",
    "    Wave1DPeriodic\n",
    "    container for the parameters of the 1D wave equation\n",
    "    We use a 'functor', i.e. a struct that behaves like a function.\n",
    "    So this function can contain data, eg parameters.\n",
    "\"\"\"\n",
    "struct Wave1DPeriodic_cpu\n",
    "    g::Float64 # gravity\n",
    "    D::Float64 # depth\n",
    "    L::Float64 # length\n",
    "    dx::Float64 # spatial step\n",
    "    nx::Int64 # number of spatial points\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    initial state for the 1D wave equation\n",
    "    We use ComponentArrays to store the state variables\n",
    "    x.h : height\n",
    "    x.u : velocity\n",
    "    The data is stored on a regular but staggered grid\n",
    "    h at: 0.0, dx, 2dx, ...\n",
    "    u at: dx/2, 3dx/2, 5dx/2, ...\n",
    "\n",
    "    The initial state is a Gaussian bump in the height field\n",
    "    of height h0, centered at x_center with a width of w times the length\n",
    "    and located at c times the length.\n",
    "\"\"\"\n",
    "function initial_state_bump(f::Wave1DPeriodic_cpu, h0=1.0, w=0.05, c=0.5)\n",
    "    x_center=f.L*c\n",
    "    width=f.L*w\n",
    "    x_h = 0.0:f.dx:(f.L-f.dx/2)\n",
    "    h = h0.*exp.(-((x_h .- x_center).^2) ./ (2 * width^2))\n",
    "    u = zeros(f.nx) .+ 0.0\n",
    "    x = ComponentVector(h=h,u=u)\n",
    "    return x\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "   Compute the spatial derivative of h\n",
    "   function dh_dx!(∂h∂x,h,dx)\n",
    "\"\"\"\n",
    "function dh_dx!(∂h∂x,h,dx)\n",
    "    nx=length(h)\n",
    "    for i in 1:(nx-1)\n",
    "        ∂h∂x[i] = (h[i+1]-h[i])/(dx)\n",
    "    end\n",
    "    ∂h∂x[end] = (h[1]-h[end])/dx\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "   Compute the spatial derivative of u\n",
    "   function du_dx!(∂u∂x,u,dx)\n",
    "\"\"\"\n",
    "function du_dx!(∂u∂x,u,dx)\n",
    "    nx=length(u)\n",
    "    for i in 2:nx\n",
    "        ∂u∂x[i] = (u[i]-u[i-1])/(dx)\n",
    "    end\n",
    "    ∂u∂x[1] = (u[1]-u[end])/dx\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    Wave1DPeriodic\n",
    "    Compute time derivative of the state\n",
    "\"\"\"\n",
    "function (f::Wave1DPeriodic_cpu)(dx_dt,x,p,t)\n",
    "    dx=f.dx\n",
    "    g=f.g\n",
    "    D=f.D\n",
    "    # temporary variables\n",
    "    ∂h∂x = similar(x.h) # allocating version, is not optimal for performance\n",
    "    ∂u∂x = similar(x.u)\n",
    "    # compute spatial derivatives\n",
    "    dh_dx!(∂h∂x,x.h,dx)\n",
    "    du_dx!(∂u∂x,x.u,dx)\n",
    "    # compute time derivatives\n",
    "    @. dx_dt.u = -g * ∂h∂x\n",
    "    @. dx_dt.h = -D * ∂u∂x\n",
    "end\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# setting up the problem\n",
    "# parameters\n",
    "nx=200     # number of spatial points\n",
    "L=10000.0  # length of the domain\n",
    "dx=L/nx    # spatial step\n",
    "D=10.0     # depth of the water\n",
    "g=10.0     # acceleration due to gravity\n",
    "\n",
    "# initial state\n",
    "f=Wave1DPeriodic_cpu(g,D,L,dx,nx)\n",
    "x0=initial_state_bump(f,1.0,0.05,0.5)\n",
    "\n",
    "# time span\n",
    "t_end=1000.0\n",
    "\n",
    "# spatial grid (for plotting)\n",
    "x_h = 0.0:f.dx:(f.L-f.dx/2)\n",
    "x_u = (f.dx/2):f.dx:f.L\n",
    "x=ComponentVector(h=x_h,u=x_u)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# plot the initial state\n",
    "p1=plot(x_h, x0.h, label=\"h(t=0)\", xlabel=\"x\", ylabel=\"h\")\n",
    "p2=plot(x_u, x0.u, label=\"u(t=0)\", xlabel=\"x\", ylabel=\"u\")\n",
    "plot(p1, p2, layout=(2,1), size=(800,600), title=\"Initial state of the 1D wave equation\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "prob = ODEProblem(f, x0, (0.0, t_end))\n",
    "@time sol = solve(prob, Tsit5(),saveat=10.0) # with timing, but ouput only at t=10.0\n",
    "sol = solve(prob, Tsit5()) # store output, no timing\n",
    "\n",
    "nothing"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# create a movie of the solution\n",
    "@time begin\n",
    "    # create a movie of the solution\n",
    "    anim = @animate for i in 1:length(sol.t)\n",
    "        p1 = plot(x_h, sol[i].h, label=\"h(t=$(round(sol.t[i],digits=1))\", xlabel=\"x\", ylabel=\"h\", ylim=(-2, 2))\n",
    "        p2 = plot(x_u, sol[i].u, label=\"u(t=$(round(sol.t[i],digits=1))\", xlabel=\"x\", ylabel=\"u\", ylim=(-2, 2))\n",
    "        plot(p1, p2, layout=(2,1), size=(800,600), title=\"Solution of the 1D wave equation\")\n",
    "    end\n",
    "    gif(anim, \"wave1d_periodic.gif\", fps=30)\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# save the solution to a JLD2 file\n",
    "cfl_out=1.0 # CFL number for output\n",
    "dt_out=cfl_out*f.dx/(sqrt(f.D*f.g)) # time step for output, based on CFL condition\n",
    "times=collect(0.0:dt_out:t_end) # output times\n",
    "solution=[]\n",
    "for t in times\n",
    "    push!(solution, sol(t))\n",
    "end\n",
    "# save the solution to a JLD2 file\n",
    "save(\"wave1d_periodic.jld2\", \n",
    "    \"solution\", solution,\n",
    "    \"times\", times,\n",
    "    \"params\", (g=f.g, D=f.D, L=f.L, dx=f.dx, nx=f.nx),\n",
    "    \"grid\", x,\n",
    "    \"cfl_out\", cfl_out,\n",
    "    \"dt_out\", dt_out,\n",
    "    \"description\", \"Solution of the 1D wave equation with periodic boundary conditions, using a Gaussian bump as initial condition.\")\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# load the solution from the JLD2 file for testing\n",
    "ff=load(\"wave1d_periodic.jld2\")\n",
    "\n",
    "@show ff[\"solution\"][1].h[1:20:end] # show some values of the initial height field"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.5",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
